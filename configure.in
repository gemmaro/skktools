dnl -*- mode: m4 -*-
dnl Process this file with autoconf to produce a configure script.
AC_INIT(skkdic-count.c)
AC_CONFIG_HEADER(config.h)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_MAKE_SET
AC_PATH_PROG(BZIP2, bzip2,, $PATH)
AC_PATH_PROG(DATE, date,, $PATH)
AC_PATH_PROG(GZIP, gzip,, $PATH)
AC_PATH_PROG(MD5, md5,, $PATH)
if test "x$ac_cv_path_MD5" = x; then
    AC_PATH_PROG(MD5, md5sum,, $PATH)
fi
AC_PATH_PROG(TAR, tar,, /usr/local/bin:$PATH)
dnl Checks for executable suffix.
AC_EXEEXT

dnl Checks for libraries.

AC_ARG_WITH(gdbm,
[  --with-gdbm             use gdbm instead of ndbm],
[WITH_GDBM="$withval"])

if test "$WITH_GDBM" = yes; then
    AC_CHECK_LIB(gdbm, dbm_open)
fi

if test "x$LIBS" = x; then
    AC_CHECK_FUNC(dbm_open,, AC_CHECK_LIB(ndbm, dbm_open))
    if test "$ac_cv_func_dbm_open" = no; then
	if test "x$LIBS" = x; then
	    AC_CHECK_LIB(db, __db_ndbm_open)
	fi
	if test "x$LIBS" = x; then
	    AC_CHECK_LIB(gdbm, dbm_open)
	fi
    fi
fi

PKG_CHECK_MODULES(GLIB2, glib-2.0 >= 2.0.0, HAVE_GLIB2=yes, HAVE_GLIB2=no)
case "$HAVE_GLIB2" in
	yes) SKKDIC_EXPR2="skkdic-expr2\${EXEEXT}" ;;
	no)  SKKDIC_EXPR2="" ;;
esac
AC_SUBST(GLIB2_LIBS)
AC_SUBST(GLIB2_CFLAGS)
AC_SUBST(SKKDIC_EXPR2)

dnl Checks for header files.
AC_CHECK_HEADERS(fcntl.h)
AC_CHECK_HEADERS(malloc.h)

if test "$ac_cv_func_dbm_open" = yes || test "$ac_cv_lib_ndbm_dbm_open" = yes; then
    AC_CHECK_HEADERS(ndbm.h)
    if test "$ac_cv_header_ndbm_h" = no; then
	AC_CHECK_HEADERS(db1/ndbm.h)
    fi
fi

dnl Checks for typedefs, structures, and compiler characteristics.

dnl Checks for library functions.
AC_TYPE_SIGNAL

AC_OUTPUT(Makefile)
